buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'jacoco-android'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        applicationId "com.thunderclouddev.tirforgoodreads"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            testCoverageEnabled false
        }
        ci {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    dataBinding.enabled = true
}

dependencies {
    def supportLibVersion = "25.2.0"
    def requeryVersion = "1.2.0"
     compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':RxSocialConnect-HawkEncryption')

    // Data binding
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    compile 'com.android.support.constraint:constraint-layout:1.0.1'
    kapt "com.android.databinding:compiler:$gradle_version"

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // Replace fragments
    compile 'com.bluelinelabs:conductor:2.0.4'

    // RxJava
    compile "io.reactivex.rxjava2:rxjava:2.0.2"
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // Automatic Rx disposal
    compile 'com.uber.autodispose:autodispose:0.1.0'
    compile 'com.uber.autodispose:autodispose-android:0.1.0'

    // Networking
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    // Xml serialization
    compile('com.squareup.retrofit2:converter-simplexml:2.2.0') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    // RxJava for Retrofit
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    // Image loading
    compile 'com.squareup.picasso:picasso:2.5.1'

    // Database
    compile "io.requery:requery:$requeryVersion"
    compile "io.requery:requery-android:$requeryVersion" // for android
    compile "io.requery:requery-kotlin:$requeryVersion"
    kapt "io.requery:requery-processor:$requeryVersion"

    // OAuth
    compile 'com.github.VictorAlbertos.RxSocialConnect-Android:core:1.0.1-2.x'
    compile 'com.github.VictorAlbertos.Jolyglot:gson:0.0.3'
    // RxSC dependencies
    compile 'com.github.scribejava:scribejava-apis:3.2.0'
    compile "com.github.VictorAlbertos:RxActivityResult:0.4.1-2.x"
    compile 'com.github.VictorAlbertos.Jolyglot:api:0.0.3'
    compile 'com.orhanobut:hawk:2.0.1'

    // Backport Java 8 datetime package
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.5'

    // Debug using chrome dev tools
    debugCompile 'com.facebook.stetho:stetho:1.4.1'
    // Find memory leaks
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}